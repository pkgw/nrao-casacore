import os
import sys

# Put the package name here, e.g. casa, tables
PACKAGE = "coordinates"
DEPS = ["casa", "tables", "scimath_f", "scimath", "measures", "fits"]

# general options
opts = Variables( 'options.cfg', ARGUMENTS )
opts.Add(PathVariable("casacoreroot", "The location of casacore/casa",
                    "/usr/local"))
opts.Add(("lapackroot", 
          "The root directory where lapack is installed", None))
opts.Add(("lapacklibdir", "The lapack library location", None))
opts.Add(("lapacklib",
          "The lapack library name (e.g. for specialized AMD libraries",
          None))
opts.Add(("blasroot", 
          "The root directory where blas is installed", None))
opts.Add(("blaslibdir", "The blas library location", None))
opts.Add(("blaslib",
          "The blas library name (e.g. for specialized AMD libraries",
          None))
opts.Add(("cfitsioroot", 
          "The root directory where cfistio is installed", None))
opts.Add(("cfitsiolibdir", "The cfitsio library location", None))
opts.Add(("wcsroot", 
          "The root directory where wcs is installed", None))
opts.Add(("wcslibdir", "The wcs library location", None))
opts.Add(("wcsincdir", "The wcs header file location", None))

env = Environment(
		  ENV = { 'PATH' : os.environ[ 'PATH' ],
			  'HOME' : os.environ[ 'HOME' ]			  
			  },
		  variables=opts
		  )
# keep a local sconsign database, rather than in very directory
env.SConsignFile()

env["PACKAGE"] = PACKAGE

# use directory from casacore/casa installation
env["casaincdir"] = [os.path.join(env["casacoreroot"],"include","casacore")]
env["casalibdir"] = [os.path.join(env["casacoreroot"],"lib")]
env["casashrdir"] = [os.path.join(env["casacoreroot"],"share","casacore")]
env["ENV"]["AIPSPATH"] = env["casacoreroot"]    

env.Tool('installer', env["casashrdir"])

env.Tool('casaoptions', env["casashrdir"])
# Add common options
env.AddCommonOptions(opts)
env.AddFortranOptions(opts)
# add installer options, e.g. prefix
env.AddInstallerOptions( opts )
# add them into environment
opts.Update( env )
# cache them for the next run
opts.Save( 'options.cfg', env)
Help( opts.GenerateHelpText( env ) )
env.Tool('buildenv', env["casashrdir"])
env.Tool('utils', env["casashrdir"])
env.Tool('casa', env["casashrdir"])
env.Tool('assaytest', env["casashrdir"])
env["ASSAYCOM"] = os.path.join(env["casashrdir"][0], "casacore_assay")

# Auto configure
if not env.GetOption('clean'):
    conf = Configure(env)
    conf.env.CheckFortran(conf)
    if not conf.CheckLib("m"): Exit(1)
    conf.env.PrependUnique(LIBS=["m"])
    # test for blas/lapack
    blasname = conf.env.get("blaslib", "blas").split(",")
    conf.env.AddCustomPackage("blas")
    blasname.reverse()
    for b in blasname:
        if not conf.CheckLib(b, autoadd=0): Exit(1)
        conf.env.PrependUnique(LIBS=b)
    lapackname = conf.env.get("lapacklib", "lapack").split(",")
    conf.env.AddCustomPackage("lapack")
    lapackname.reverse()
    for l in lapackname:
        if not conf.CheckLib(l, autoadd=0): Exit(1)
        conf.env.PrependUnique(LIBS=l)
    # test for cfitsio
    conf.env.AddCustomPackage('cfitsio')
    if not conf.CheckLib('cfitsio', autoadd=0):
        Exit(1)
    conf.env.PrependUnique(LIBS=['cfitsio'])

    conf.env.AddCustomPackage('wcs')
    if not conf.CheckLibWithHeader('wcs', 'wcslib/wcs.h', language='c',
				   autoadd=0):
	Exit(1)
    conf.env.PrependUnique(LIBS=['wcs'])

    if not conf.CheckLib("dl"): Exit(1)
    conf.env.PrependUnique(LIBS=['dl'])
    env = conf.Finish()
else:
    Execute(Delete("options.cfg"))

# create the installer which handles installing the final build
installer = env.Installer()

# to find package based includes
env.Append(CPPPATH=['#'])
env.AppendUnique(CPPPATH=env["casaincdir"])
env.AppendUnique(LIBPATH=env["casalibdir"])
for dep in DEPS:
    env.Prepend(LIBS=["casa_"+dep])

for bopt in env["build"]:
    # create an environment copy with the dbg/opt compiler flags
    buildenv = env.BuildEnv(bopt)
    buildenv["buildtype"] = bopt
    # buildir name
    buildenv["BUILDDIR"] = Dir("#/build_%s/%s" % (env.PlatformIdent(), bopt))
    env.SConscript(["%s/SConscript" % env["PACKAGE"]], 
		   build_dir= buildenv["BUILDDIR"],
		   duplicate=0, exports=["buildenv", "installer"]) 
