import os
import sys

# This is now needed to make sure assaytest work.
EnsureSConsVersion(0,97)

# Put the package name here, e.g. casa, tables
PACKAGE="casa"

# general options
opts = Options( 'options.cfg', ARGUMENTS )
opts.Add(BoolOption("enable_hdf5", "Turn on hdf5 support", False))
opts.Add(("hdf5root", 
	  "The root directory where hdf5 is installed", None))
opts.Add(("hdf5libdir", "The hdf5 library location", None))

env = Environment(ENV = { 'PATH' : os.environ[ 'PATH' ],
			  'HOME' : os.environ[ 'HOME' ] 
			  },
		  options=opts
		  )
# keep a local sconsign database, rather than in very directory
env.SConsignFile()

env["PACKAGE"] = PACKAGE
# use build in dir
env["casashrdir"] = ["./tools"]
env["casalibdir"] = None
env["casaincdir"] = None

env.Tool('casaoptions', env["casashrdir"])
# Add common options
env.AddCommonOptions(opts)
# add installer options, e.g. prefix
env.Tool('installer', env["casashrdir"])
env.AddInstallerOptions( opts )
# add them into environment
opts.Update( env )
# cache them for the next run
opts.Save( 'options.cfg', env)
Help( opts.GenerateHelpText( env ) )

env.Tool('buildenv', env["casashrdir"])
env.Tool('casa', env["casashrdir"])
env.Tool('utils', env["casashrdir"])
env.Tool('assaytest', env["casashrdir"])

env["ENV"]["AIPSPATH"] = env["prefix"]

# Auto configure
if not env.GetOption('clean'):
    conf = Configure(env)
    if conf.env["enable_hdf5"]:
        conf.env.AddCustomPackage('hdf5')
        if conf.CheckLib('hdf5', autoadd=0):
            conf.env.PrependUnique(LIBS=['hdf5'])  
            conf.env.PrependUnique(CPPFLAGS=['-DHAVE_HDF5'])
        else:
            Exit(1)
    env = conf.Finish()
else:
    Execute(Delete("options.cfg"))
# create the installer which handles installing the final build
installer = env.Installer()

# to find package based includes
env.Append(CPPPATH='#')

# Replace some builder commands (lex,yacc) with custom versions
env.CustomCasaCom()

for bopt in env["build"]:
    # create an environment copy with the dbg/opt compiler flags
    buildenv = env.BuildEnv(bopt)
    # buildir name
    buildenv["BUILDDIR"] = Dir("#/build_%s/%s" % (env.PlatformIdent(), bopt))
    env.SConscript(["%s/SConscript" % env["PACKAGE"]], 
		   build_dir= buildenv["BUILDDIR"],
		   duplicate=0, exports=["buildenv", "installer"])

# add the Tools to the casacore/share directory. This way they can be imported
# by the other casacore packages without having to duplicate them.
installer.AddShares("tools", "*.py", "casacore/", True)
installer.AddShares("tools", "casacore_assay", "casacore/")
installer.AddShares("tools", "floatcheck.sh", "casacore/")
